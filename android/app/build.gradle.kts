import java.util.Properties
import java.io.FileInputStream

plugins {
    id("com.android.application")
    id("kotlin-android")
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id("dev.flutter.flutter-gradle-plugin")
}

val keystorePropertiesFile = rootProject.file("key.properties")
val keystoreProperties = Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(FileInputStream(keystorePropertiesFile))
}

android {
    namespace = "com.mrdarksidetm.wallet"
    compileSdk = 36
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlin {
        jvmToolchain(17)
    }

    defaultConfig {
        applicationId = "com.mrdarksidetm.wallet"
        minSdk = 33
        targetSdk = 36
        ndk {
           abiFilters.add("armeabi-v7a")
           abiFilters.add("arm64-v8a")
        }
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }
    
    splits {
        abi {
            isEnable = true
            reset()
            include("armeabi-v7a", "arm64-v8a")
            isUniversalApk = true            
        }
    }

    signingConfigs {
        create("release") {
            val rawAlias = keystoreProperties.getProperty("keyAlias")
            val rawKeyPass = keystoreProperties.getProperty("keyPassword")
            val rawStorePass = keystoreProperties.getProperty("storePassword")
            val rawStoreFile = keystoreProperties.getProperty("storeFile")

            keyAlias = rawAlias?.trim()
            storePassword = rawStorePass?.trim()
            keyPassword = rawKeyPass?.trim()?.takeIf { it.isNotEmpty() } ?: storePassword

            if (rawStoreFile != null && rawStoreFile.trim().isNotEmpty()) {
                storeFile = rootProject.file(rawStoreFile.trim())
            }
        }
    }

    buildTypes {
        release {
            signingConfig = signingConfigs.getByName("release")
        }
    }
}

flutter {
    source = "../.."
}
